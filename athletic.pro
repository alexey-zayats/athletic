include(athletic.pri)

!minQtVersion(5, 5, 0) {
	message("Cannot build Athletic with Qt version $${QT_VERSION}.")
	error("Use at least Qt 5.5.0.")
}

include(doc/doc.pri)

TEMPLATE = subdirs
SUBDIRS = \
	src \
	share \


contains(QT_ARCH, i386): ARCHITECTURE = x86
else: ARCHITECTURE = $$QT_ARCH

macx: PLATFORM = "mac"
else:win32: PLATFORM = "windows"
else:linux-*: PLATFORM = "linux-$${ARCHITECTURE}"
else: PLATFORM = "unknown"

BASENAME = $$(INSTALL_BASENAME)
isEmpty(BASENAME): BASENAME = athletic-$${PLATFORM}$(INSTALL_EDITION)-$${APP_VERSION}$(INSTALL_POSTFIX)

macx:INSTALLER_NAME = "athletic-$${APP_VERSION}"
else:INSTALLER_NAME = "$${BASENAME}"

macx {
	APPBUNDLE = "$$OUT_PWD/bin/Athletic.app"
	BINDIST_SOURCE = "$$OUT_PWD/bin/Athletic.app"
	BINDIST_INSTALLER_SOURCE = $$BINDIST_SOURCE
	deployqt.commands = $$PWD/share/scripts/deployqtHelper_mac.sh \"$${APPBUNDLE}\" \"$$[QT_INSTALL_TRANSLATIONS]\" \"$$[QT_INSTALL_PLUGINS]\" \"$$[QT_INSTALL_IMPORTS]\" \"$$[QT_INSTALL_QML]\"
	codesign.commands = codesign --deep -s \"$(SIGNING_IDENTITY)\" $(SIGNING_FLAGS) \"$${APPBUNDLE}\"
	dmg.commands = $$PWD/share/scripts/makedmg.sh $$OUT_PWD/bin $${BASENAME}.dmg
	#dmg.depends = deployqt
	QMAKE_EXTRA_TARGETS += codesign dmg
} else {
	BINDIST_SOURCE = "$(INSTALL_ROOT)$$APP_PREFIX"
	BINDIST_INSTALLER_SOURCE = "$$BINDIST_SOURCE/*"
	deployqt.commands = python -u $$PWD/scripts/deployqt.py -i \"$(INSTALL_ROOT)$$APP_PREFIX\" \"$(QMAKE)\"
	deployqt.depends = install
	win32 {
		# vcredist_msvc2013
		deployartifacts.depends = install
		deployartifacts.commands = git clone "git://code.qt.io/qt-creator/binary-artifacts.git" -b $$BINARY_ARTIFACTS_BRANCH&& xcopy /s /q /y /i "binary-artifacts\\win32" \"$(INSTALL_ROOT)$$QTC_PREFIX\"&& rmdir /s /q binary-artifacts
		QMAKE_EXTRA_TARGETS += deployartifacts
	}
}

INSTALLER_ARCHIVE_FROM_ENV = $$(INSTALLER_ARCHIVE)
isEmpty(INSTALLER_ARCHIVE_FROM_ENV) {
	INSTALLER_ARCHIVE = $$OUT_PWD/$${BASENAME}-installer-archive.7z
} else {
	INSTALLER_ARCHIVE = $$OUT_PWD/$$(INSTALLER_ARCHIVE)
}

#bindist.depends = deployqt
bindist.commands = 7z a -mx9 $$OUT_PWD/$${BASENAME}.7z \"$$BINDIST_SOURCE\"
#bindist_installer.depends = deployqt
bindist_installer.commands = 7z a -mx9 $${INSTALLER_ARCHIVE} \"$$BINDIST_INSTALLER_SOURCE\"
installer.depends = bindist_installer
installer.commands = python -u $$PWD/scripts/packageIfw.py -i \"$(IFW_PATH)\" -v $${QTCREATOR_VERSION} -a \"$${INSTALLER_ARCHIVE}\" "$$INSTALLER_NAME"

macx {
	codesign_installer.commands = codesign -s \"$(SIGNING_IDENTITY)\" $(SIGNING_FLAGS) \"$${INSTALLER_NAME}.app\"
	dmg_installer.commands = hdiutil create -srcfolder "$${INSTALLER_NAME}.app" -volname \"Qt Creator\" -format UDBZ "$${BASENAME}-installer.dmg" -ov -scrub -size 1g -verbose
	QMAKE_EXTRA_TARGETS += codesign_installer dmg_installer
}

win32 {
	deployqt.commands ~= s,/,\\\\,g
	bindist.commands ~= s,/,\\\\,g
	bindist_installer.commands ~= s,/,\\\\,g
	installer.commands ~= s,/,\\\\,g
}

QMAKE_EXTRA_TARGETS += deployqt bindist bindist_installer installer


